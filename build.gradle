plugins {
    id "com.github.spotbugs" version "1.6.5"
    id 'ru.vyarus.quality' version '3.0.0'
}

apply plugin: 'java'
apply plugin: "ru.vyarus.quality"

quality {
    exclude '**/generated/*'
    sourceSets = [project.sourceSets.main, project.sourceSets.test]
    
    spotbugsVersion = '3.1.8'
    spotbugs = true // false to disable automatic plugin activation
    spotbugsEffort = 'max'  // min, less, more or max
    spotbugsLevel = 'high' // low, medium, high

    checkstyle = false
    pmd = false
}

repositories {
    mavenCentral()
}

// most of the jar libraries is available in the /dependencies folder, not in a remote repository
dependencies {
    compile fileTree(dir: 'dependencies', include: ['*.jar'])
    compile 'com.google.guava:guava:26.0-jre'
    compile 'com.github.spotbugs:spotbugs-annotations:3.1.8'
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.11.0'
}

// the lines bellow deal with exporting a running jar
jar {
    manifest {
        attributes 'Main-Class': 'br.ufpe.cin.app.JFSTMerge'
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

artifacts {
    archives fatJar
}

task copyBinary(type: Copy, dependsOn: assemble, group: "Binaries", description: "Copies binaries to /binary folder.") {
    from fatJar
    into "binary"
    rename "jFSTMerge.*", "jFSTMerge.jar"
}

task updateInstaller(type: Copy, dependsOn: assemble, group: "Binaries", description: "Updates installer jar.") {
    from fatJar
    into "installer"
    rename "jFSTMerge.*", "s3m.jar"
    
    doLast {
        File installer = file("installer/s3mInstaller.jar")
        if(!installer.exists() || installer.length() < 5000) {
            exec {
                commandLine("git", "lfs", "pull", "-I", "installer/s3mInstaller.jar")
            }
        }

        ant.jar(update: "true", destfile: installer) {
            zipfileset(file: "installer/s3m.jar")
        }
        
        delete "installer/s3m.jar"
    }
}
build.finalizedBy(copyBinary, updateInstaller)

// Disabling asserts.
tasks.withType(Test) {
    enableAssertions = false
}
